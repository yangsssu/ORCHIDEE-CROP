! =================================================================================================================================
! MODULE        : gammad_inc
!
! CONTACT       : orchidee-help _at_ ipsl.jussieu.fr
!
! LICENCE       : IPSL (2006)
! This software is governed by the CeCILL licence see ORCHIDEE/ORCHIDEE_CeCILL.LIC
!
!>\BRIEF        This module computes the parameters for TOPMODEL, gamma distribution fitting. 
!!
!!\n DESCRIPTION : contains.
!! 
!! RECENT CHANGE(S) : None
!!
!! REFERENCE(S) : None
!!
!! SVN          :
!! $HeadURL: svn://forge.ipsl.jussieu.fr/orchidee/perso/shushi.peng/ORCHIDEE/src_sechiba/gammad_inc.f90 $
!! $Date: 2015-03-10 10:16:04 +0100 (Tue, 10 Mar 2015) $
!! $Revision: 2535 $
!! \n
!_ ================================================================================================================================

!C######################################################################
!C######################################################################
!C
!C       THIS ROUTINE COMPUTE THE INCOMPLETE AND THE COMPLEMENTARY 
!C INCOMPLETE GAMMA FUNCTION DOUBLE PRECISION. IN BRIEF, THIS 
!C FUNCTION HELP TO COMPUTE THE DIFFERENT FRACTION PRESENT IN THE 
!C TOPMODEL FRAMEWORK. 
!C       MORE EXPLANATION ON THE SUBROUTINE USE ARE GIVEN IN THE NEXT 
!C COMMENTARY 
!C
!C THIS ROUTINE WAS FOUND ON http://www.netlib.org WEB SITE.
!C      
!C REFERENCE - W. GAUTSCHI, :: A COMPUTATIONAL PROCEDURE FOR INCOMPLETE 
!C GAMMA FUNCTIONS, ACM TRANS. MATH. SOFTWARE., (1979) 482-489
!C
!C######################################################################
!C######################################################################
!C
!C     LET  GAMMA(A)  DENOTE THE GAMMA FUNCTION AND  GAM(A,X)  THE
!C (COMPLEMENTARY) INCOMPLETE GAMMA FUNCTION,
!C
!C    GAM(A,X)=INTEGRAL FROM T=X TO T=INFINITY OF EXP(-T)*T**(A-1).
!C
!C LET  GAMSTAR(A,X)  DENOTE TRICOMI:S FORM OF THE INCOMPLETE GAMMA
!C FUNCTION, WHICH FOR A.GT.0. IS DEFINED BY
!C
!C  GAMSTAR(A,X)=(X**(-A)/GAMMA(A))*INTEGRAL FROM T=0 TO T=X OF
!C                EXP(-T)*T**(A-1),
!C
!C AND FOR A.LE.0. BY ANALYTIC CONTINUATION. FOR THE PURPOSE OF
!C THIS SUBROUTINE, THESE FUNCTIONS ARE NORMALIZED AS FOLLOWS&
!C
!C             GAM(A,X)/GAMMA(A),  IF A.GT.0.,
!C     G(A,X)=
!C             EXP(X)*X**(-A)*GAM(A,X),  IF A.LE.0.,
!C
!C     GSTAR(A,X)=(X**A)*GAMSTAR(A,X)
!C               =(1/GAMMA(A))*INTEGRAL FROM T=0 TO T=X OF EXP(-T)*T**(A-1)
!C
!C THE PROGRAM BELOW ATTEMPTS TO EVALUATE  G(A,X)  AND  GSTAR(A,X),
!C BOTH TO AN ACCURACY OF ACC SIGNIFICANT DECIMAL DIGITS, FOR ARBI-
!C TRARY REAL VALUES OF  A  AND NONNEGATIVE VALUES OF  X. THE SUB-
!C ROUTINE AUTOMATICALLY CHECKS FOR UNDERFLOW AND OVERFLOW CONDI-
!C TIONS AND RETURNS APPROPRIATE WARNINGS THROUGH THE OUTPUT PARA-
!C METERS  IFLG, IFLGST. A RESULT THAT UNDERFLOWS IS RETURNED WITH
!C THE VALUE 0., ONE THAT OVERFLOWS WITH THE VALUE OF THE LARGEST
!C MACHINE-REPRESENTABLE NUMBER.
!C
!C     NEAR LINES IN THE (A,X)-PLANE, A.LT.0., ALONG WHICH  GSTAR
!C VANISHES, THE ACCURACY SPECIFIED WILL BE ATTAINED ONLY IN TERMS
!C OF ABSOLUTE ERROR, NOT RELATIVE ERROR. THERE ARE OTHER (RARE)
!C INSTANCES IN WHICH THE ACCURACY ATTAINED IS SOMEWHAT LESS THAN
!C THE ACCURACY SPECIFIED. THE DISCREPANCY, HOWEVER, SHOULD NEVER
!C EXCEED ONE OR TWO (DECIMAL) ORDERS OF ACCURACY# NO INDICATION
!C OF THIS IS GIVEN THROUGH ERROR FLAGS.
!C
!C     PARAMETER LIST&
!C
!C        A - - THE FIRST ARGUMENT OF G AND GSTAR. TYPE REAL.
!C        X - - THE SECOND ARGUMENT OF G AND GSTAR. TYPE REAL.
!C      ACC - - THE NUMBER OF CORRECT SIGNIFICANT DECIMAL DIGITS
!C              DESIRED IN THE RESULTS. TYPE REAL.
!C        G - - AN OUTPUT VARIABLE RETURNING THE VALUE OF G(A,X).
!C              TYPE REAL.
!C    GSTAR - - AN OUTPUT VARIABLE RETURNING THE VALUE OF
!C              GSTAR(A,X). TYPE REAL.
!C     IFLG - - AN ERROR FLAG INDICATING A NUMBER OF ERROR CONDI-
!C              TIONS IN G UPON EXECUTION. TYPE INTEGER.
!C!   IFLGST - - AN ERROR FLAG INDICATING A NUMBER OF ERROR CONDI-
!C              TIONS IN GSTAR UPON EXECUTION. TYPE INTEGER.
!C              THE VALUES OF IFLG AND IFLGST HAVE THE FOLLOWING
!C              MEANINGS&
!C              0 - NO ERROR CONDITION.
!C              1 - ILLEGAL NEGATIVE ARGUMENT X. THE ROUTINE EXITS
!C                  WITH THE VALUES ZERO FOR G AND GSTAR.
!C              2 - INFINITELY LARGE RESULT AT X=0. THE ROUTINE
!C                  RETURNS THE LARGEST MACHINE-REPRESENTABLE NUMBER.
!C              3 - (ONLY FOR IFLGST) GSTAR IS INDETERMINATE AT
!C                  A=0. AND X=0. THE ROUTINE RETURNS THE VALUE 1.,
!C                  WHICH IS THE LIMIT VALUE AS X TENDS TO ZERO FOR
!C                  FIXED A=0.
!C              4 - THE RESULT UNDERFLOWS. IT IS SET EQUAL TO 0.
!C              5 - THE RESULT OVERFLOWS. IT IS SET EQUAL TO THE
!C                  LARGEST MACHINE-REPRESENTABLE NUMBER, WITH
!C                  PROPER SIGN.
!C              6 - CONVERGENCE FAILS WITHIN 600 ITERATIONS, EITHER
!C                  IN TAYLOR:S SERIES OR IN LEGENDRE:S CONTINUED
!C                  FRACTION. REASON UNKNOWN. THE COMPUTATION IS
!C                  ABORTED AND THE ROUTINE RETURNS WITH ZERO
!C                  VALUES FOR G AND GSTAR.
!C
!C     ALL MACHINE-DEPENDENT PARAMETERS ARE COLLECTED IN THE FIRST
!C DATA DECLARATION. THEY ARE AS FOLLOWS&
!C
!C     PREC - - THE SINGLE PRECISION ACCURACY, TO BE SET APPROXI-
!C              MATELY EQUAL TO BETA*ALOG(2.)/ALOG(10.), WHERE BETA
!C              IS THE NUMBER OF BINARY DIGITS AVAILABLE IN THE MAN-
!C              TISSA OF THE SINGLE PRECISION FLOATING-POINT WORD.
!C              TYPE REAL.
!C   TOPEXP - - APPROXIMATELY THE LARGEST POSITIVE NUMBER T SUCH
!C              THAT 10.**T IS STILL REPRESENTABLE ON THE COMPUTER
!C              IN SINGLE PRECISION FLOATING-POINT ARITHMETIC.
!C              TYPE REAL.
!C   BOTEXP - - APPROXIMATELY THE SMALLEST NEGATIVE NUMBER T SUCH
!C              THAT 10.**T IS STILL REPRESENTABLE ON THE COMPUTER
!C              IN SINGLE PRECISION FLOATING-POINT ARITHMETIC.
!C              TYPE REAL.
!C
!C IN THE PROGRAM BELOW THESE PARAMETERS ARE SET TO CORRESPOND TO
!C THE MACHINE CHARACTERISTICS OF THE CDC 6500 COMPUTER.
!C
!C     THE SECOND DATA DECLARATION CONTAINS THE SINGLE PRECISION
!C VALUE OF ALOG(10.). THE NEXT DATA DECLARATION CONTAINS THE SUCCES-
!C SIVE COEFFICIENTS IN THE MACLAURIN EXPANSION OF (1/GAMMA(A+1))-1.
!C THEY ARE GIVEN TO AS MANY DECIMAL PLACES AS IS NECESSARY TO ACHIEVE
!C MACHINE PRECISION (ON THE CDC 6500 COMPUTER) IN THE RANGE
!C ABS(A).LE..5. MORE ACCURATE VALUES OF THESE COEFFICIENTS (TO
!C 31 DECIMAL PLACES) CAN BE FOUND IN TABLE 5 OF J.W.WRENCH,JR.,
!C CONCERNING TWO SERIES FOR THE GAMMA FUNCTION, MATH. COMPUT.
!C 22, 1968, 617-626.
!C
!C     THE SUBROUTINE CALLS ON A FUNCTION SUBROUTINE, NAMED  ALGA,
!C WHICH IS TO PROVIDE SINGLE PRECISION VALUES OF THE LOGARITHM OF
!C THE GAMMA FUNCTION FOR ARGUMENTS LARGER THAN OR EQUAL TO .5.
!C A POSSIBLE VERSION OF SUCH A FUNCTION SUBROUTINE IS APPENDED
!C TO THE PRESENT SUBROUTINE. IT IS TAYLORED TO THE ACCURACY RE-
!C QUIREMENTS OF THE CDC 6500 COMPUTER, AND USES RATIONAL APPROXI-
!C MATIONS DUE TO CODY AND HILLSTROM (MATH. COMPUT. 21, 1967, 198-
!C 203).
!C
!C     REFERENCE - W. GAUTSCHI, ::A COMPUTATIONAL PROCEDURE FOR
!C INCOMPLETE GAMMA FUNCTIONS, ACM TRANS. MATH. SOFTWARE.
!C
!!     ################     
      MODULE gammad_inc 
!     ################
!
!
      INTERFACE
!      
!
      SUBROUTINE DGAM(A, X, ACC, G, GSTAR, IFLG, IFLGST)                
      
        DOUBLE PRECISION :: A, X, G, GSTAR, C, AL10, ALX, ALPHA, ALPREC, &
     & TOP, BOT, AINF, EPS, EPS1, ES, SGA, AE, AA, AP1, AM1, AEP1, &
     & AEM1, FMA, AEPS, SGAE, AAEPS, ALGP1, SGGA, ALGEP1, SGGS, ALGS, &
     & ALG, SUMM, GA, Y, TERM, U, P, Q, R, V, T, H, SGT, A1X, RHO, XPA, &
     & XMA, S, DLGA
!
      END
!
      END INTERFACE
!
      END MODULE gammad_inc 
!
!################################################################
!
      SUBROUTINE DGAM(A, X, ACC, G, GSTAR, IFLG, IFLGST)                
!
!################################################################
      DOUBLE PRECISION :: A, X, G, GSTAR, C, AL10, ALX, ALPHA, ALPREC, &
     & TOP, BOT, AINF, EPS, EPS1, ES, SGA, AE, AA, AP1, AM1, AEP1, &
     & AEM1, FMA, AEPS, SGAE, AAEPS, ALGP1, SGGA, ALGEP1, SGGS, ALGS, &
     & ALG, SUMM, GA, Y, TERM, U, P, Q, R, V, T, H, SGT, A1X, RHO, XPA, &
     & XMA, S, DLGA
      DIMENSION C(29)
      DATA PREC, TOPEXP, BOTEXP /15.9545,308.,-308./
      DATA AL10 /2.3025850929940456840179914547D0/
      DATA C /.57721566490153286060651209008D0, &
     &       -.65587807152025388107701951515D0, &
     &       -4.200263503409523552900393488D-2, &
     &        .1665386113822914895017007951D0, &
     &       -4.21977345555443367482083013D-2, &
     &       -9.6219715278769735621149217D-3, &
     &       7.2189432466630995423950103D-3, &
     &       -1.165167591859065112113971D-3, &
     &       -2.15241674114950972815730D-4, &
     &       1.2805028238811618615320D-4, &
     &       -2.013485478078823865569D-5, &
     &       -1.2504934821426706573D-6, &
     &       1.1330272319816958824D-6, &
     &       -2.056338416977607103D-7, &
     &       6.1160951044814158D-9, &
     &       5.0020076444692229D-9, &
     &       -1.181274570487020D-9, &
     &       1.04342671169110D-10, &
     &       7.782263439905D-12, &
     &       -3.696805618642D-12, &
     &       5.1003702875D-13, &
     &       -2.058326054D-14, -5.34812254D-15, &
     &       1.2267786D-15, -1.181259D-16,1.187D-18, &
     &       1.412D-18,-2.30D-19,1.7D-20/ 
      G = 0.D0
      GSTAR = 0.D0
      IF (X.LT.0.D0) GO TO 290
!C
!C INITIALIZATION
!C
      IFLG = 0
      IFLGST = 0
      I = 0
      IF (X.GT.0.D0) ALX = DLOG(X)
      ALPHA = X + .25D0
      IF (X.LT..25D0 .AND. X.GT.0.D0) ALPHA = DLOG(.5D0)/ALX
!C      ALPREC = AL10*DBLE(PREC)
!C      TOP    = AL10*DBLE(TOPEXP)
!C      BOT    = AL10*DBLE(BOTEXP)
      ALPREC =DBLE(DIGITS(1.D0))*DLOG(2.D0)
      TOP    =DLOG10(HUGE(1.D0))
      BOT    =DLOG10(TINY(1.D0))
      AINF   = 10.D0**TOPEXP
      EPS = .5D0*10.D0**(-ACC)
      EPS1 = EPS/1.D2
      SGA = 1.D0
      IF (A.LT.0.D0) SGA = -SGA
      AE = A
      AA = DABS(A)
      AP1 = A + 1.D0
      AEP1 = AP1
      MA = SNGL(.5D0-A)
      FMA = DBLE(FLOAT(MA))
      AEPS = A + FMA
      SGAE = 1.D0
      IF (AEPS.LT.0.D0) SGAE = -SGAE
      AAEPS = DABS(AEPS)
      ALGP1 = 0.D0
!C
!C EVALUATION OF THE LOGARITHM OF THE ABSOLUTE VALUE OF
!C GAMMA(A+1.) AND DETERMINATION OF THE SIGN OF GAMMA(A+1.)
!C
      SGGA = 1.D0
      IF (MA.LE.0) GO TO 10
      IF (AEPS.EQ.0.D0) GO TO 20
      SGGA = SGAE
      IF (MA.EQ.2*(MA/2)) SGGA = -SGGA
      ALGP1 = DLGA(AEPS+1.D0) - DLOG(AAEPS)
      IF (MA.EQ.1) GO TO 20
      ALGP1 = ALGP1 + DLGA(1.D0-AEPS) - DLGA(FMA-AEPS)
      GO TO 20
   10 ALGP1 = DLGA(AP1)
   20 ALGEP1 = ALGP1
      IF (X.GT.0.D0) GO TO 60
!C
!C EVALUATION OF GSTAR(A,0.) AND G(A,0.)
!C
      IF (A) 30, 40, 50
   30 IFLGST = 2
      GSTAR = AINF
      G = 1.D0/AA
      RETURN
   40 IFLGST = 3
      GSTAR = 1.D0
      IFLG = 2
      G = AINF
      RETURN
   50 G = 1.D0
      RETURN
   60 IF (A.GT.ALPHA) GO TO 220
      IF (X.GT.1.5D0) GO TO 240
      IF (A.LT.-.5D0) GO TO 170
!C
!C DIRECT EVALUATION OF G(A,X) AND GSTAR(A,X) FOR X.LE.1.5
!C AND -.5.LE.A.LE.ALPHA(X)
!C
      GSTAR = 1.D0
      IF (A.GE..5D0) GO TO 110
   70 SUMM = C(29)
      DO 80 K=1,28
        K1 = 29 - K
        SUMM = AE*SUMM + C(K1)
   80 CONTINUE
      GA = -SUMM/(1.D0+AE*SUMM)
      Y = AE*ALX
      IF (DABS(Y).GE.1.D0) GO TO 100
      SUMM = 1.D0
      TERM = 1.D0
      K = 1
   90 K = K + 1
      IF (K.GT.600) GO TO 330
      TERM = Y*TERM/DBLE(FLOAT(K))
      SUMM = SUMM + TERM
      IF (DABS(TERM).GT.EPS1*SUMM) GO TO 90
      U = GA - SUMM*ALX
      GO TO 120
  100 U = GA - (DEXP(Y)-1.D0)/AE
      GO TO 120
  110 U = DEXP(DLGA(A)) - (X**A)/A
  120 P = AE*X
      Q = AEP1
      R = AE + 3.D0
      TERM = 1.D0
      SUMM = 1.D0
      K = 1
  130 K = K + 1
      IF (K.GT.600) GO TO 330
      P = P + X
      Q = Q + R
      R = R + 2.D0
      TERM = -P*TERM/Q
      SUMM = SUMM + TERM
      IF (DABS(TERM).GT.EPS1*SUMM) GO TO 130
      V = (X**AEP1)*SUMM/AEP1
      G = U + V
      IF (I.EQ.1) GO TO 180
      IF (A) 140, 150, 160
  140 T = DEXP(X)*X**(-A)
      G = T*G
      GSTAR = 1.D0 - A*G*DEXP(-ALGP1)/T
      RETURN
  150 G = DEXP(X)*G
      RETURN
  160 G = A*G*DEXP(-ALGP1)
      GSTAR = 1.D0 - G
      RETURN
!C
!C RECURSIVE EVALUATION OF G(A,X) FOR X.LE.1.5 AND A.LT.-.5
!C
  170 I = 1
      AE = AEPS
      AEP1 = AEPS + 1.D0
      IF (X.LT..25D0 .AND. AE.GT.ALPHA) GO TO 210
      GO TO 70
  180 G = G*DEXP(X)*X**(-AE)
      DO 190 K=1,MA
        G = (1.D0-X*G)/(DBLE(FLOAT(K))-AE)
  190 CONTINUE
      ALG = DLOG(G)
!C
!C EVALUATION OF GSTAR(A,X) IN TERMS OF G(A,X)
!C
  200 GSTAR = 1.D0
      IF (MA.GE.0 .AND. AEPS.EQ.0.D0) RETURN
      SGT = SGA*SGGA
      T = DLOG(AA) - X + A*ALX + ALG - ALGP1
      IF (T.LT.-ALPREC) RETURN
      IF (T.GE.TOP) GO TO 320
      GSTAR = 1.D0 - SGT*DEXP(T)
      RETURN
  210 I = 2
      ALGEP1 = DLGA(AEP1)
!C
!C EVALUATION OF GSTAR(A,X) FOR A.GT.ALPHA(X) BY TAYLOR
!C EXPANSION
!C
  220 G = 1.D0
      TERM = 1.D0
      SUMM = 1.D0
      K = 0
  230 K = K + 1
      IF (K.GT.600) GO TO 340
      TERM = X*TERM/(AE+DBLE(FLOAT(K)))
      SUMM = SUMM + TERM
      IF (DABS(TERM).GT.EPS*SUMM) GO TO 230
      ALGS = AE*ALX - X + DLOG(SUMM) - ALGEP1
      IF (ALGS.LE.BOT) GO TO 310
      GSTAR = DEXP(ALGS)
      G = 1.D0 - GSTAR
      IF (I.NE.2) RETURN
      G = G*DEXP(ALGEP1)/AE
      GO TO 180
!C
!C EVALUATION OF G(A,X) FOR X.GT.1.5 AND A.LE.ALPHA(X) BY
!C MEANS OF THE LEGENDRE CONTINUED FRACTION
!C
  240 GSTAR = 1.D0
      XPA = X + 1.D0 - A
      XMA = X - 1.D0 - A
      P = 0.D0
      Q = XPA*XMA
      R = 4.D0*XPA
      S = -A + 1.D0
      TERM = 1.D0
      SUMM = 1.D0
      RHO = 0.D0
      K = 1
  250 K = K + 1
      IF (K.GT.600) GO TO 330
      P = P + S
      Q = Q + R
      R = R + 8.D0
      S = S + 2.D0
      T = P*(1.D0+RHO)
      RHO = T/(Q-T)
      TERM = RHO*TERM
      SUMM = SUMM + TERM
      IF (DABS(TERM).GT.EPS*SUMM) GO TO 250
      IF (A) 260, 270, 280
  260 G = SUMM/XPA
      ALG = DLOG(G)
      GO TO 200
  270 G = SUMM/XPA
      RETURN
  280 ALG = A*ALX - X + DLOG(A*SUMM/XPA) - ALGP1
      IF (ALG.LE.BOT) GO TO 300
      G = DEXP(ALG)
      GSTAR = 1.D0 - G
      RETURN
  290 IFLG = 1
      IFLGST = 1
      RETURN
  300 IFLG = 4
      RETURN
  310 IFLGST = 4
      RETURN
  320 IFLGST = 5
      GSTAR = -SGT*AINF
      RETURN
  330 IFLG = 6
      RETURN
  340 IFLGST = 6
      RETURN
      END
      
      DOUBLE PRECISION FUNCTION DLGA(DX)                                
      DOUBLE PRECISION DBNUM, DBDEN, DX, DC, DP, DY, DT, DS
      DIMENSION DBNUM(8), DBDEN(8)
      DATA DBNUM /-3.617D3,1.D0,-6.91D2,1.D0,-1.D0,1.D0,-1.D0,1.D0/, &
     & DBDEN /1.224D5,1.56D2,3.6036D5,1.188D3,1.68D3,1.26D3,3.6D2,1.2D1/
      DC = .5D0*DLOG(8.D0*DATAN(1.D0))
      DP = 1.D0
      DY = DX
      Y = SNGL(DY)
!C
!C THE CONDITIONAL CLAUSE IN THE NEXT STATEMENT EXPRESSES THE
!C INEQUALITY Y.GT.EXP(.121189*DPREC+.053905), WHERE DPREC IS THE
!C NUMBER OF DECIMAL DIGITS CARRIED IN DOUBLE PRECISION FLOATING-POINT
!C ARITHMETIC.
!C
   10 IF (Y.GT.35.027) GO TO 20
      DP = DY*DP
      DY = DY + 1.D0
      Y = SNGL(DY)
      GO TO 10
   20 DT = 1.D0/(DY*DY)
      DS = 4.3867D4/2.44188D5
      DO 30 I=1,8
        DS = DT*DS + DBNUM(I)/DBDEN(I)
   30 CONTINUE
      DLGA = (DY-.5D0)*DLOG(DY) - DY + DC + DS/DY - DLOG(DP)
      RETURN
      END


