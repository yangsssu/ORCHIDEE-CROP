#!/usr/bin/perl
# ------------------------------------------------------------------------------
# (C) Crown copyright Met Office. All rights reserved.
# For further details please refer to the file COPYRIGHT.txt
# which you should have received as part of this distribution.
# ------------------------------------------------------------------------------

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";
use FCM::Admin::System qw{
    backup_svn_repository
    filter_projects
    get_projects_from_svn_live
};
use FCM::Admin::Util qw{option2config};
use Getopt::Long qw{GetOptions};
use Pod::Usage qw{pod2usage};

main();

sub main {
    my %option;
    my $result = GetOptions(
        \%option,
        q{help|usage|h},
        q{no-housekeep-dumps},
        q{no-verify-integrity},
        q{svn-backup-dir=s},
        q{svn-dump-dir=s},
        q{svn-live-dir=s},
        q{svn-project-suffix=s},
    );
    if (!$result) {
        pod2usage(1);
    }
    if (exists($option{help})) {
        pod2usage(q{-verbose} => 1);
    }
    option2config(\%option);
    my @projects = filter_projects([get_projects_from_svn_live()], \@ARGV);
    for my $project (@projects) {
        backup_svn_repository(
            $project,
            !$option{q{no-housekeep-dumps}},
            !$option{q{no-verify-integrity}},
        );
    }
}

__END__

=head1 NAME

fcm-backup-svn-repos

=head1 SYNOPSIS

    fcm-backup-svn-repos [OPTIONS] [PROJECT ...]

=head1 OPTIONS

=over 4

=item --help, -h, --usage

Prints help and exits.

=item --no-housekeep-dumps

If this option is specified, the program will not housekeep the backup revision
dumps of each repository.

=item --no-verify-integrity

If this option is specified, the program will not verify the integrity of a
repository before running the backup.

=item --svn-backup-dir=DIR

Specifies the root location of the backup directory. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-dump-dir=DIR

Specifies the root location of the directory where revision dumps are kept. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-live-dir=DIR

Specifies the root location of the live directory. See
L<FCM::Admin::Config|FCM::Admin::Config> for the current default.

=item --svn-project-suffix=NAME

Specifies the suffix added to the project name. The default is "_svn".

=back

=head1 ARGUMENTS

=over 4

=item PROJECT

Specifies one or more project to back up. If no project is specified, the
program searches the live directory for projects to back up.

=back

=head1 DESCRIPTION

This program archives Subversion repositories in the live directory to the
backup directory.

=head1 COPYRIGHT

E<169> Crown copyright Met Office. All rights reserved.

=cut
